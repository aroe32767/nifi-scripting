buildscript {
    repositories {
        mavenCentral()
        maven {
            url 'http://dl.bintray.com/sponiro/gradle-plugins'
        }
    }
    dependencies {
        classpath group: 'de.fanero.gradle.plugin.nar', name: 'gradle-nar-plugin', version: '0.1'
    }
}

apply plugin: 'idea'

description = 'NiFi Dynamic Script Executors'

allprojects {

    apply plugin: 'java'

    group = 'org.apache.nifi'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenCentral()
        mavenLocal()
    }
}

def narProjects = subprojects - project(':nifi-sumo-common')

configure(narProjects) {

    apply plugin: 'de.fanero.gradle.plugin.nar'

    dependencies {
        compile project(':nifi-sumo-common')
        compile "org.apache.nifi:nifi-api:${nifiVersion}"
        compile "org.apache.nifi:nifi-processor-utils:${nifiVersion}"

        testCompile "org.apache.nifi:nifi-mock:${nifiVersion}"
        testCompile "commons-io:commons-io:2.4"
        testCompile "junit:junit:4.+"
        testCompile "org.slf4j:slf4j-simple:1.7.12"
    }
}


task deploy(dependsOn: 'nar', type: Copy) {
  description 'Deploys assembled "nar" archive to Apache NiFi installation. Will call "nar" task to assemble nar archive.'
  if (project.hasProperty('nifi_home')) {
     from 'build/libs/'
     into nifi_home + '/lib'
     include ('*.nar')
     println("Deploying '" + project.name + "' to " + (nifi_home + '/lib'))
  } 
  else {
     if (gradle.startParameter.taskNames.toString().contains('deploy')){
        throw new java.lang.IllegalArgumentException("Failed to determine the value of 'nifi_home' property. Possible reasons: " +
       "Property is not defined. You can define it in either 'gradle.properties' file or via command line argument 'gradlew deploy -Pnifi_home=/foo/bar/nifi'.")
     }
  }
}

task wrapper(type: Wrapper) {
    gradleVersion = gradleWrapperVersion
}
